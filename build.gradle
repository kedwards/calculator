buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.19.RELEASE'
	}
}

plugins {
	id 'java'
}

apply plugin: 'org.springframework.boot'

group = 'com.kedwards'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	testCompile 'org.springframework.boot:spring-boot-starter-test'

	// Redis client library
	compile "org.springframework.data:spring-data-redis:1.8.0.RELEASE"
	compile "redis.clients:jedis:2.9.0"

	// Java cucumber libraries
	testCompile("info.cukes:cucumber-java:1.2.4")
    testCompile("info.cukes:cucumber-junit:1.2.4")
}

// jacoco for test coverage analysis, others Clover, and Cobertura
// fail if coverage < 40% 
apply plugin: "jacoco"
jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.4
			}
		}
	}
}

// checkstyle for static analysis on the Java code, others FindBugs, and PMD
apply plugin: 'checkstyle'

// Gradle cucumber acceptance, This splits the tests into:
// unit (run with ./gradlew test) 
// and
// acceptance (run with ./gradlew acceptanceTest)
task acceptanceTest(type: Test) {
    include '**/acceptance/**'
    systemProperties System.getProperties()
}

test {
    exclude '**/acceptance/**'
}

// make the build JAR executable, so that it can be easily run on the
// server as a Unix service
bootRepackage {
    executable = true
}